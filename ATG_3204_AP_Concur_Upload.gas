Program.Sub.ScreenSU.Start
gui.F_AP_UPLOAD..create
gui.F_AP_UPLOAD..caption("Invoice Upload from Concur")
gui.F_AP_UPLOAD..size(4110,2580)
gui.F_AP_UPLOAD..position(0,0)
gui.F_AP_UPLOAD..event(resize,f_ap_upload_resize)
gui.F_AP_UPLOAD..event(unload,f_ap_upload_unload)
gui.F_AP_UPLOAD..alwaysontop(False)
gui.F_AP_UPLOAD..fontname("Arial")
gui.F_AP_UPLOAD..fontsize(8)
gui.F_AP_UPLOAD..forecolor(0)
gui.F_AP_UPLOAD..fontstyle(,,,,)
gui.F_AP_UPLOAD..BackColor(-2147483633)
gui.F_AP_UPLOAD..controlbox(True)
gui.F_AP_UPLOAD..maxbutton(True)
gui.F_AP_UPLOAD..minbutton(True)
gui.F_AP_UPLOAD..mousepointer(0)
gui.F_AP_UPLOAD..moveable(True)
gui.F_AP_UPLOAD..sizeable(True)
gui.F_AP_UPLOAD..ShowInTaskBar(True)
gui.F_AP_UPLOAD..titlebar(True)
gui.F_AP_UPLOAD.txtFile.create(textbox,"",True,3000,300,0,200,400,True,0,Arial,8,-2147483643,1)
gui.F_AP_UPLOAD.dtpBatch.create(datepicker)
gui.F_AP_UPLOAD.dtpBatch.visible(True)
gui.F_AP_UPLOAD.dtpBatch.size(1935,300)
gui.F_AP_UPLOAD.dtpBatch.zorder(0)
gui.F_AP_UPLOAD.dtpBatch.position(200,955)
gui.F_AP_UPLOAD.dtpBatch.enabled(True)
gui.F_AP_UPLOAD.dtpBatch.checkbox(False)
gui.F_AP_UPLOAD.lbl1.create(label,"File",True,1935,240,1,200,200,True,0,Arial,8,-2147483633,0)
gui.F_AP_UPLOAD.lbl2.create(label,"Batch Date",True,1935,255,1,200,730,True,0,Arial,8,-2147483633,0)
gui.F_AP_UPLOAD.cmdBrowser.create(button)
gui.F_AP_UPLOAD.cmdBrowser.caption("^")
gui.F_AP_UPLOAD.cmdBrowser.visible(True)
gui.F_AP_UPLOAD.cmdBrowser.size(375,375)
gui.F_AP_UPLOAD.cmdBrowser.zorder(0)
gui.F_AP_UPLOAD.cmdBrowser.position(3215,360)
gui.F_AP_UPLOAD.cmdBrowser.enabled(True)
gui.F_AP_UPLOAD.cmdBrowser.fontname("Arial")
gui.F_AP_UPLOAD.cmdBrowser.fontsize(8)
gui.F_AP_UPLOAD.cmdBrowser.event(click,cmdbrowser_click)
gui.F_AP_UPLOAD.cmdOK.create(button)
gui.F_AP_UPLOAD.cmdOK.caption("OK")
gui.F_AP_UPLOAD.cmdOK.visible(True)
gui.F_AP_UPLOAD.cmdOK.size(855,375)
gui.F_AP_UPLOAD.cmdOK.zorder(0)
gui.F_AP_UPLOAD.cmdOK.position(200,1405)
gui.F_AP_UPLOAD.cmdOK.enabled(True)
gui.F_AP_UPLOAD.cmdOK.fontname("Arial")
gui.F_AP_UPLOAD.cmdOK.fontsize(8)
gui.F_AP_UPLOAD.cmdOK.event(click,cmdok_click)


Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start


Variable.UDT.uUpload.Define("Vendor",String)
Variable.UDT.uUpload.Define("Invoice",String)
Variable.UDT.uUpload.Define("GL",String)
Variable.UDT.uUpload.Define("InvoiceDate",String)
Variable.UDT.uUpload.Define("InvoiceAmt",String)
Variable.UDT.uUpload.Define("TrnsCode",String)
Variable.UDT.uUpload.Define("Line",String)
Variable.UDT.uUpload.Define("BatchDate",String)
Variable.UDT.uUpload.Define("InvoiceDDate",String)
Variable.UDT.uUpload.Define("WO",String)
Variable.UDT.uUpload.Define("Sfx",String)
Variable.UDT.uUpload.Define("ExRate",String)
Variable.UDT.uUpload.Define("Operation",String)
Variable.uGlobal.uUpload.Declare("uUpload")
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Coded by: JLH
'Project Start Date: 8/10/2011
'Other Components:
'  Quote 2631
'Notes:
'  User will be able to select an file exported from Concur from a file browser and it will be uploaded into Global Shop Accounts Payable Open Item.
'  See example txt file attached to call. Parse through the file to create a text file called APONITM.txt for upload using UPLAPOP. See GS File Conversion.doc pages 19-20 for upload format.

F.ODBC.Connection!Conx.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)
Gui.F_AP_UPLOAD..Show

Program.Sub.Main.End

program.sub.cmdok_click.start
F.Intrinsic.Control.SetErrorHandler("Load_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sFile.Declare(String)
V.Local.sText.Declare(String)
V.Local.iRet.Declare(Long)
V.Local.bRet.Declare(Boolean)
V.Local.iCount.Declare(Long)
V.Local.sRet.Declare(String)
V.Local.sTemp.Declare(String)
V.Local.iUB.Declare(Long)
V.Local.dRet.Declare(Date)

F.Intrinsic.Debug.Stop

V.uGlobal.uUpload.Redim(-1,-1)

'F.Intrinsic.UI.ShowOpenFileDialog("",".txt","",V.Local.sFile)
V.Local.sFile.Set(V.Screen.F_AP_UPLOAD!txtFile.Text)
F.Intrinsic.File.Exists(V.local.sFile,V.Local.bRet)
F.Intrinsic.Control.If(V.Local.bRet,=,True)
	F.Intrinsic.File.File2String(V.Local.sFile,V.Local.sText)
	F.Intrinsic.String.Replace(V.Local.sText,V.Ambient.DblQuote,"",V.local.sText)
	F.Intrinsic.String.Split(V.Local.sText,V.Ambient.NewLine,V.Local.sText)
	F.Intrinsic.Control.For(V.Local.iCount,1,V.Local.sText.UBound,1)
		F.Intrinsic.Control.If(V.Local.sText(v.Local.iCount),<>,"")
			F.Intrinsic.String.Split(V.Local.sText(v.Local.iCount),",",V.Local.sRet)
			F.Intrinsic.Control.If(V.Local.sRet(1),<>,"")
				F.Intrinsic.Control.If(V.uGlobal.uUpload.UBound,=,-1)
					V.uGlobal.uUpload.Redim(0,0)
				F.Intrinsic.Control.Else
					V.Local.iUB.Set(V.uGlobal.uUpload.UBound)
					F.Intrinsic.Math.Add(V.Local.iUB,1,V.Local.iUB)
					V.uGlobal.uUpload.RedimPreserve(0,V.Local.iUB)
				F.Intrinsic.Control.EndIf
				'Vendor # and reserved 6 + 1
				F.Intrinsic.String.RPad(V.Local.sRet(1)," ",6,V.Local.sTemp)
				F.Intrinsic.Control.CallSub(Vendor_check,"Vendor",V.Local.sTemp)
				F.Intrinsic.Control.If(V.Args.Return,=,"True")
					V.uGlobal.uUpload(v.Local.iUB)!Vendor.Set(V.Local.sTemp)
				F.Intrinsic.Control.Else
					F.Intrinsic.String.Concat("Vendor ",V.Local.sRet(1)," does not exist",V.Local.sTemp)
					F.Intrinsic.UI.Msgbox(V.Local.sTemp)
					F.Intrinsic.Control.CallSub(F_ap_upload_unload)
				F.Intrinsic.Control.EndIf
				'Invoice 15
				F.Intrinsic.String.Right(V.Local.sRet(4),15,V.Local.sTemp)
				V.uGlobal.uUpload(v.Local.iUB)!Invoice.Set(V.Local.sTemp)
				'Transaction Code- always 10 (invoice) 2
				V.uGlobal.uUpload(v.Local.iUB)!TrnsCode.Set("10")
				'Batch Line 3
				F.Intrinsic.Math.Add(V.Local.iUB,1,V.Local.iRet)
				F.Intrinsic.String.LPad(V.Local.iRet,"0",3,V.Local.sTemp)
				V.uGlobal.uUpload(v.Local.iUB)!Line.Set(V.Local.sTemp)
				'Batch Date MMDDYY 6
				F.Intrinsic.String.Format(V.Screen.F_AP_UPLOAD!dtpBatch.Value,"MMDDYY",V.Local.sTemp)
				V.uGlobal.uUpload(v.Local.iUB)!BatchDate.Set(V.Local.sTemp)
				'GL Acct 15
				F.Intrinsic.String.Concat(V.Local.sRet(23),"-",V.Local.sRet(29),V.Local.sTemp)
				F.Intrinsic.String.RPad(V.Local.sTemp," ",15,V.Local.sTemp)
				F.Intrinsic.Control.CallSub(Gl_check,"GL",V.Local.sTemp)
				F.Intrinsic.Control.If(V.Args.Return,=,"True")
					V.uGlobal.uUpload(v.Local.iUB)!GL.Set(V.Local.sTemp)
				F.Intrinsic.Control.Else
					F.Intrinsic.String.Concat("GL Account ",V.Local.sTemp," does not exist",V.Local.sTemp)
					F.Intrinsic.UI.Msgbox(V.Local.sTemp)
					F.Intrinsic.Control.CallSub(F_ap_upload_unload)
				F.Intrinsic.Control.EndIf
				'Invoice Date and filler MMDDYY 6+5
				F.Intrinsic.String.Format(V.local.sRet(10),"MMDDYY",V.Local.sTemp)
				V.uGlobal.uUpload(v.Local.iUB)!InvoiceDate.Set(V.Local.sTemp)
				'Invoice Amt 16
				F.Intrinsic.String.Replace(V.Local.sRet(22),"$","",V.Local.sTemp)
				F.Intrinsic.String.Format(V.Local.sTemp,"##############.00",V.Local.sTemp)
				V.uGlobal.uUpload(v.Local.iUB)!InvoiceAmt.Set(V.Local.sTemp)
				'Invoice Due Date MMDDYY 6
'				F.Intrinsic.Debug.Stop
				V.Local.dRet.Set(V.local.sRet(0))
				'F.Intrinsic.String.Format(V.Local.sRet(0),"YYYYMMDD",V.Local.dRet)
				F.Intrinsic.Date.DateAdd("D",3,V.Local.dRet,V.Local.sTemp)
				F.Intrinsic.String.Format(V.Local.sTemp,"MMDDYY",V.Local.sTemp)
				V.uGlobal.uUpload(v.Local.iUB)!InvoiceDDate.Set(V.Local.sTemp)
				'Job/WO 6
				F.Intrinsic.String.RPad(V.Local.sRet(29)," ",6,V.Local.sTemp)
				V.uGlobal.uUpload(v.Local.iUB)!WO.Set(V.Local.sTemp)
				'Suffix 3
				'F.Intrinsic.String.RPad(V.Local.sRet(30)," ",3,V.Local.sTemp)
				V.uGlobal.uUpload(v.Local.iUB)!Sfx.Set("   ")
				'Operation (sequence) 6
				F.Intrinsic.Control.If(V.Local.sRet(29),<>,"")
					F.Intrinsic.String.RPad("400"," ",6,V.Local.sTemp)
					V.uGlobal.uUpload(v.Local.iUB)!Operation.Set(V.Local.sTemp)
				F.Intrinsic.Control.Else
					F.Intrinsic.String.RPad(" "," ",6,V.Local.sTemp)
					V.uGlobal.uUpload(v.Local.iUB)!Operation.Set(V.Local.sTemp)
				F.Intrinsic.Control.EndIf
				'Currency Code 3	
				V.uGlobal.uUpload(v.Local.iUB)!ExRate.Set(V.Local.sRet(5))
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Next(V.Local.iCount)
F.Intrinsic.Control.EndIf

F.Intrinsic.Debug.Stop

F.Intrinsic.Control.CallSub(Buildfile)

F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.Label("Load_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_AP_Concur_Upload.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


program.sub.cmdok_click.end

program.sub.cmdbrowser_click.start
F.Intrinsic.Control.SetErrorHandler("Load_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sFile.Declare(String)
V.Local.bRet.Declare(Boolean)


'F.Intrinsic.Debug.Stop

F.Intrinsic.UI.ShowOpenFileDialog("",".csv","",V.Local.sFile)
F.Intrinsic.File.Exists(V.Local.sFile,V.Local.bRet)
F.Intrinsic.Control.If(V.Local.bRet,=,True)
	Gui.F_AP_UPLOAD.txtFile.text(V.Local.sFile)
F.Intrinsic.Control.EndIf


F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.Label("Load_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_AP_Concur_Upload.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

program.sub.cmdbrowser_click.end

program.sub.f_ap_upload_unload.start
f.Intrinsic.Control.End

program.sub.f_ap_upload_unload.end

program.sub.f_ap_upload_resize.start

program.sub.f_ap_upload_resize.end

Program.Sub.buildfile.Start
F.Intrinsic.Control.SetErrorHandler("Load_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sFile.Declare(String)
V.Local.sText.Declare(String)
V.Local.bRet.Declare(Boolean)
V.Local.iCount.Declare(Long)
V.Local.sTemp.Declare(String)

F.Intrinsic.Debug.Stop

F.Intrinsic.Control.For(V.Local.iCount,0,V.uGlobal.uUpload.UBound,1)
	F.Intrinsic.Control.If(V.Local.iCount,<>,0)
		F.Intrinsic.String.Concat(V.Local.sText,V.Ambient.NewLine,V.Local.sText)
	F.Intrinsic.Control.EndIf
	'Vendor # and reserved 6 + 1
	F.Intrinsic.String.RPad(V.uGlobal.uUpload(v.Local.iCount)!Vendor," ",7,V.Local.sTemp)
	F.Intrinsic.String.Concat(V.local.sText,V.Local.sTemp,V.Local.sText)
	'Invoice 15
	F.Intrinsic.String.Concat(V.Local.sText,V.uGlobal.uUpload(v.Local.iCount)!Invoice,V.local.sText)
	'Transaction Code- always 10 (invoice) 2
	F.Intrinsic.String.Concat(V.Local.sText,V.uGlobal.uUpload(v.Local.iCount)!TrnsCode,V.local.sText)
	'Batch # 5, Batch Line 3
	F.Intrinsic.String.LPad(V.uGlobal.uUpload(v.Local.iCount)!Line," ",8,V.Local.sTemp)
	F.Intrinsic.String.Concat(V.local.sText,V.Local.sTemp,V.Local.sText)
	'Batch Date MMDDYY 6
	F.Intrinsic.String.Concat(V.Local.sText,V.uGlobal.uUpload(v.Local.iCount)!BatchDate,V.local.sText)
	'Territory/Branch & Area 2+2
	F.Intrinsic.String.Concat(V.Local.sText,"    ",V.local.sText)
	'GL Acct 15
	F.Intrinsic.String.Concat(V.Local.sText,V.uGlobal.uUpload(v.Local.iCount)!GL,V.local.sText)
	'Invoice Date and filler MMDDYY 6+5
	F.Intrinsic.String.RPad(V.uGlobal.uUpload(v.Local.iCount)!InvoiceDate," ",11,V.Local.sTemp)
	F.Intrinsic.String.Concat(V.local.sText,V.Local.sTemp,V.Local.sText)
	'Invoice Amt 16
	F.Intrinsic.String.LPad(V.uGlobal.uUpload(v.Local.iCount)!InvoiceAmt," ",16,V.Local.sTemp)
	F.Intrinsic.String.Concat(V.local.sText,V.Local.sTemp,V.Local.sText)
	'Invoice Due Date MMDDYY 6
	F.Intrinsic.String.Concat(V.Local.sText,V.uGlobal.uUpload(v.Local.iCount)!InvoiceDDate,V.local.sText)
	'Filler, Inv Discount, PO #, Buyer, Check #, Check Date, Check Code, Receiver #
	F.Intrinsic.String.RPad(" "," ",50,V.Local.sTemp)
	F.Intrinsic.String.Concat(V.Local.sText,V.Local.sTemp,V.Local.sText)
	'Job/WO and suffix 6 + 3
	F.Intrinsic.String.Concat(V.Local.sText,V.uGlobal.uUpload(v.Local.iCount)!WO,V.uGlobal.uUpload(v.Local.iCount)!Sfx,V.local.sText)
	'Operation(sequence) 6
	F.Intrinsic.String.Concat(V.Local.sText,V.uGlobal.uUpload(v.Local.iCount)!Operation,V.local.sText)
	'Filler
	F.Intrinsic.String.RPad(" "," ",115,V.Local.sTemp)
	F.Intrinsic.String.Concat(V.Local.sText,V.Local.sTemp,V.Local.sText)
	'Currency Code 3
	F.Intrinsic.String.Concat(V.Local.sText,V.uGlobal.uUpload(v.Local.iCount)!ExRate,V.local.sText)
	'Filler
	F.Intrinsic.String.RPad(" "," ",61,V.Local.sTemp)
	F.Intrinsic.String.Concat(V.Local.sText,V.Local.sTemp,V.Local.sText)
F.Intrinsic.Control.Next(V.Local.iCount)

F.Intrinsic.Debug.Stop

F.Intrinsic.String.Concat(V.caller.FilesDir,"\APOPNITM.TXT",V.Local.sFile)
F.Intrinsic.File.String2File(V.Local.sFile,V.Local.sText)
F.Global.General.CallWrapperSync(50,"UPLAPOP/a")


F.Intrinsic.Control.CallSub(F_ap_upload_unload)


F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.Label("Load_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_AP_Concur_Upload.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.buildfile.End

Program.Sub.Vendor_Check.Start
F.Intrinsic.Control.SetErrorHandler("Vendor_Check_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.local.sQuery.Declare(String)
V.Local.sRet.Declare(String)

F.Intrinsic.String.Concat("Select * From VENDOR_MASTER where VENDOR='",V.Args.Vendor.Trim,"'",V.Local.sQuery)
F.ODBC.Connection!Conx.OpenRecordsetRO("rst",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.Conx!rst.EOF,=,True)
	V.Local.sRet.Set("False")
F.Intrinsic.Control.Else
	V.local.sRet.Set("True")
F.Intrinsic.Control.EndIf
F.ODBC.Conx!rst.Close

F.Intrinsic.Variable.AddRV("Return",V.Local.sRet)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Vendor_Check_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_AP_Concur_Upload.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.Vendor_Check.End

Program.Sub.GL_Check.Start
F.Intrinsic.Control.SetErrorHandler("GL_Check_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)

V.local.sQuery.Declare(String)
V.Local.sRet.Declare(String)

F.Intrinsic.String.Concat("Select * From GL_MASTER where GL_ACCOUNT='",V.Args.GL.Trim,"'",V.Local.sQuery)
F.ODBC.Connection!Conx.OpenRecordsetRO("rst",V.Local.sQuery)
F.Intrinsic.Control.If(V.ODBC.Conx!rst.EOF,=,True)
	V.Local.sRet.Set("False")
F.Intrinsic.Control.Else
	V.local.sRet.Set("True")
F.Intrinsic.Control.EndIf
F.ODBC.Conx!rst.Close

F.Intrinsic.Variable.AddRV("Return",V.Local.sRet)
F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("GL_Check_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_AP_Concur_Upload.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.GL_Check.End


